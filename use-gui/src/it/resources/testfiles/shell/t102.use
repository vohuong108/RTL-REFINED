model CivilStatusSnapshotWorld

enum CivilStatus {single, married, divorced, widowed}
enum Gender {female, male}

class Person

attributes
  name:String
  civstat:CivilStatus
  gender:Gender
  alive:Boolean

operations

-- birth(aName:String, aGender:Gender)
-- pre  freshUnlinkedPerson: name.isUndefined and civstat.isUndefined and
--      gender.isUndefined and alive.isUndefined and
--      wife.isUndefined and husband.isUndefined
-- post nameAssigned: name=aName
-- post civstatAssigned: civstat=#single
-- post genderAssigned: gender=aGender
-- post isAliveAssigned: alive=true

-- marry(aSpouse:Person)
-- pre  aSpouseDefined: aSpouse.isDefined
-- pre  isAlive: alive
-- pre  aSpouseAlive: aSpouse.alive
-- pre  isUnmarried: civstat<>#married
-- pre  aSpouseUnmarried: aSpouse.civstat<>#married
-- pre  differentGenders: gender<>aSpouse.gender
-- post isMarried: civstat=#married
-- post femaleHasMarriedHusband: gender=#female implies
--      husband=aSpouse and husband.civstat=#married
-- post maleHasMarriedWife: gender=#male implies
--      wife=aSpouse and wife.civstat=#married

-- divorce()
-- pre  isMarried: civstat=#married
-- pre  isAlive: alive
-- pre  husbandAlive: gender=#female implies husband.alive
-- pre  wifeAlive: gender=#male implies wife.alive
-- post isDivorced: civstat=#divorced
-- post husbandDivorced: gender=#female implies
--      husband.isUndefined and husband@pre.civstat=#divorced
-- post wifeDivorced: gender=#male implies
--      wife.isUndefined and wife@pre.civstat=#divorced

-- death()
-- pre  isAlive: alive
-- post notAlive: not(alive)
-- post husbandWidowed: gender=#female and husband@pre.isDefined implies
--      husband@pre.wife.isUndefined and husband@pre.civstat=#widowed
-- post wifeWidowed: gender=#male and wife@pre.isDefined implies
--      wife@pre.husband.isUndefined and wife@pre.civstat=#widowed

spouse():Person=if gender=#female then husband else wife endif

-- constraints
--   inv attributesDefined: name.isDefined and civstat.isDefined and
--       gender.isDefined and alive.isDefined
--   inv nameCapitalThenSmallLetters:
--       let small:Set(String)=
--         Set{'a','b','c','d','e','f','g','h','i','j','k','l','m',
--             'n','o','p','q','r','s','t','u','v','w','x','y','z'} in
--       let capital:Set(String)=
--         Set{'A','B','C','D','E','F','G','H','I','J','K','L','M',
--             'N','O','P','Q','R','S','T','U','V','W','X','Y','Z'} in
--       capital->includes(name.substring(1,1)) and
--       Set{2..name.size}->forAll(i |
--         small->includes(name.substring(i,i)))
--   inv nameIsUnique: Person.allInstances->forAll(self2|
--       self<>self2 implies self.name<>self2.name)
--   inv femaleHasNoWife: gender=#female implies wife.isUndefined
--   inv maleHasNoHusband: gender=#male implies husband.isUndefined

end

class Snapshot
constraints
inv inv_attributesDefined:
  self.person->forAll(self1|
    (self1.name.isDefined and self1.civstat.isDefined and
     self1.gender.isDefined and self1.alive.isDefined) xor
    (self1.name.isUndefined and self1.civstat.isUndefined and
     self1.gender.isUndefined and self1.alive.isUndefined))
inv inv_nameCapitalThenSmallLetters:
  self.person->forAll(self1|
    let
	small:Set(String) =
      Set{'a','b','c','d','e','f','g','h','i','j','k','l','m',
          'n','o','p','q','r','s','t','u','v','w','x','y','z'},
    capital:Set(String) =
      Set{'A','B','C','D','E','F','G','H','I','J','K','L','M',
          'N','O','P','Q','R','S','T','U','V','W','X','Y','Z'} in
    (capital->includes(self1.name.substring(1,1)) and
     Set{2..self1.name.size}->forAll(i |
       small->includes(self1.name.substring(i,i)))) xor
    self1.name.isUndefined)
inv inv_nameIsUnique:
  self.person->forAll(self1|self.person->forAll(self2|
    self1<>self2 implies self1.name<>self2.name))
inv inv_femaleHasNoWife:
  self.person->forAll(self1|
    self1.gender=#female implies self1.wife.isUndefined)
inv inv_maleHasNoHusband:
  self.person->forAll(self1|
    self1.gender=#male implies self1.husband.isUndefined)
end

class SubSnapshot < Snapshot
end

association Marriage between
  Person [0..1] role wife
  Person [0..1] role husband
end

association SnapshotPerson between
  Snapshot [1] role snapshot
  Person [0..*] role person
end

abstract associationclass OpC between
  Snapshot [0..1] role pred
  Snapshot [0..1] role succ
end

abstract associationclass PersonOpC < OpC
attributes
  aSelf:Person
end

associationclass birthC < PersonOpC
attributes
  aName:String
  aGender:Gender
constraints

inv birth_pre_freshUnlinkedPerson:
   aSelf.name.isUndefined and aSelf.civstat.isUndefined and
   aSelf.gender.isUndefined and aSelf.alive.isUndefined and
   aSelf.wife.isUndefined and aSelf.husband.isUndefined
   
inv birth_pos_nameAssigned:
   aSelf.succ.name = self.aName
   
inv birth_pos_civstatAssigned:
   aSelf.succ.civstat=#single
   
inv birth_pos_genderAssigned:
   aSelf.succ.gender=aGender
   
inv birth_pos_isAliveAssigned:
   aSelf.succ.alive=true
   
end

associationclass marryC < PersonOpC
attributes
  aSpouse:Person
constraints
inv marry_pre_aSpouseDefined:
   aSpouse.isDefined
   
inv marry_pre_isAlive:
   aSelf.alive
   
inv marry_pre_aSpouseAlive:
   aSpouse.alive
   
inv marry_pre_isUnmarried:
   aSelf.civstat<>#married
   
inv marry_pre_aSpouseUnmarried:
   aSpouse.civstat<>#married
   
inv marry_pre_differentGenders:
   aSelf.gender<>aSpouse.gender
   
inv marry_pos_isMarried:
   aSelf.succ.civstat=#married
   
inv marry_pos_femaleHasMarriedHusband:
   aSelf.succ.gender=#female implies
   aSelf.succ.husband=aSpouse.succ and 
   aSelf.succ.husband.civstat=#married
   
inv marry_pos_maleHasMarriedWife:
   aSelf.succ.gender=#male implies
   aSelf.succ.wife=aSpouse.succ and 
   aSelf.succ.wife.civstat=#married

end

associationclass divorceC < PersonOpC

constraints
inv divorce_pre_isMarried:
  aSelf.civstat=#married
  
inv divorce_pre_isAlive:
   aSelf.alive
   
inv divorce_pre_husbandAlive:
   aSelf.gender=#female implies aSelf.husband.alive
   
inv divorce_pre_wifeAlive:
   aSelf.gender=#male implies aSelf.wife.alive
   
inv divorce_pos_isDivorced:
   aSelf.succ.civstat=#divorced
   
inv divorce_pos_husbandDivorced:
   aSelf.succ.gender=#female implies
     aSelf.succ.husband.isUndefined and
     aSelf.husband.succ.civstat=#divorced
	 
inv divorce_pos_wifeDivorced:
   aSelf.succ.gender=#male implies
     aSelf.succ.wife.isUndefined and
     aSelf.wife.succ.civstat=#divorced
end

associationclass deathC < PersonOpC

constraints
inv death_pre_isAlive:
   aSelf.alive
   
inv death_pos_notAlive:
   not aSelf.succ.alive
   
inv death_pos_husbandWidowed:
   aSelf.succ.gender=#female and aSelf.husband.isDefined implies
   aSelf.husband.succ.wife.isUndefined and
   aSelf.husband.succ.civstat=#widowed
   
inv death_pos_wifeWidowed:
   aSelf.succ.gender=#male and aSelf.wife.isDefined implies
   aSelf.wife.succ.husband.isUndefined and
   aSelf.wife.succ.civstat=#widowed
------------------------------------------------------------------------
end

association PredSuccPerson between
  Person [0..1] role pred
  Person [0..1] role succ
end
